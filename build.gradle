buildscript {
    dependencies {
        classpath group: 'de.dynamicfiles.projects.gradle.plugins', name: 'javafx-gradle-plugin', version: '8.8.2'
    }
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'java'
    id 'application'
    id 'idea'
    id "org.jetbrains.kotlin.jvm" version '1.2.30'
    id 'jacoco'
}
apply plugin: 'javafx-gradle-plugin'

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

version = "0.7.1"

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8"
    compile 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.22.5'
    compile 'org.projectlombok:lombok:1.16.18'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
    compile group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.25'
    compile 'org.glassfish.jersey.core:jersey-client:2.25.1'
    compile 'com.fasterxml.jackson.core:jackson-core:2.9.2'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.9.2'
    compile 'com.fasterxml.jackson.module:jackson-module-parameter-names:2.9.2'
    compile "com.fasterxml.jackson.module:jackson-module-kotlin:2.9.2"
    compile 'com.github.sys1yagi:mastodon4j:1.6.0'
    compile 'io.github.microutils:kotlin-logging:1.4.4'
    compile "org.jetbrains.kotlin:kotlin-reflect"
    compile "org.jsoup:jsoup:1.10.3"
    compile 'no.tornado:tornadofx:1.7.11'

    testCompile "org.jetbrains.kotlin:kotlin-test"
    testCompile "org.jetbrains.kotlin:kotlin-test-junit"
    testCompile 'org.mockito:mockito-core:2.8.9'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile "org.testfx:testfx-core:4.0.7-alpha"
    testCompile "org.testfx:testfx-junit:4.0.6-alpha"
    testCompile "com.nhaarman:mockito-kotlin-kt1.1:1.5.0"

    // uncomment for headless test
   // testCompile 'org.testfx:openjfx-monocle:8u76-b04'
}

jfx {
    // minimal requirement for jfxJar-task
    mainClassName = 'com.github.wakingrufus.mastodon.MainKt'
    mainClass = 'com.github.wakingrufus.mastodon.MainKt'
    appName = "mastodon-jfx"
    // minimal requirement for jfxNative-task
    vendor = 'wakingrufus'
    bundler = "deb"
    nativeReleaseVersion = project.version
    verbose = true
}

idea {
    project {
        languageLevel = '1.8'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.6'
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

jacocoTestReport.dependsOn test
build.dependsOn jfxNative
build.dependsOn jacocoTestReport

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

test {
    // uncomment below line for headless test
  //  jvmArgs "-Djava.awt.headless=true", "-Dtestfx.headless=true", "-Dheadless=true", "-Dtestfx.robot=glass", "-Dglass.platform=Monocle", "-Dmonocle.platform=Headless", "-Dprism.order=sw", "-Dprism.text=t2k", "-Dtestfx.setup.timeout=2500"
}